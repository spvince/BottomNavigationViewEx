apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    namespace 'com.github.ittianyu.widget'
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 28
        versionName "2.0.8"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:$rootProject.appcompat"
    implementation "com.google.android.material:material:$rootProject.design"

    androidTestImplementation "androidx.test:runner:$rootProject.testRunner"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.7.0"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.junit"
}

// Encodage UTF-8
tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

// Génération du Jar des sources
tasks.register('sourcesJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

// Publier la library avec Maven
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = project.group
                artifactId = project.name
                version = android.defaultConfig.versionName

                artifact("$buildDir/outputs/aar/${project.name}-release.aar")
                artifact(sourcesJar) {
                    classifier = 'sources'
                }
            }
        }

        repositories {
            mavenLocal()                    // nécessaire pour JitPack
            maven {
                name = "localBuild"        // optionnel, pour tests locaux
                url = uri("${buildDir}/repo")
            }
        }
    }
}
